;
; Copyright (c) 2022 Piotr Stolarz
; OneWireNg: Arduino 1-wire service library
;
; Distributed under the 2-clause BSD License (the License)
; see accompanying file LICENSE for details.
;
; This software is distributed WITHOUT ANY WARRANTY; without even the
; implied warranty of MERCHANTABILITY or FITNESS FOR A PARTICULAR PURPOSE.
; See the License for more information.
;

;
; NOTES
; 1. Cycle times in standard and over-drive modes expressed in 0.1us units.
; 2. Strong and weak power pull-ups are defined as PIO opcodes switching the
;    1-wire bus into strong or weak high state.
; 3. At the end of each of the following programs PIO pushes single bit result
;    into RX FIFO, where the main driver waits for it and if gets it, turns
;    the PIO SM off. Each of the programs wrap around blocking PUSH opcode
;    (with 1 bit PUSH threshold configured) waiting for the termination.
;

;
; reset
;
.program w1_reset
.define public cycle    350     ; standard mode
.define public od_cycle 40      ; over-drive mode
.side_set 1

    set  pindirs, 1 side 0 [13] ; OUT low: 490us, 56us (OD)
    set  pindirs, 0 side 1 [1]  ; IN (high by pull-up resistor)
    in   pins, 1    side 1      ; sampling at 70us, 8us (OD) high state
    nop             side 1 [9]
.wrap_target
    push            side 1      ; 70+420us, 8+48us (OD) high-tail
                                ; push presence pulse

;
; touch-0
; TX FIFO: type of power pull-up set on the bus afterwards
;
.program w1_touch0
.define public cycle    30      ; standard mode
.define public od_cycle 4       ; overdrive mode
.define public strong   0xf001  ; PIO opcode: set  pins, 1    side 1
.define public weak     0xf080  ; PIO opcode: set  pindirs, 0 side 1
.side_set 1

    set  pindirs, 1 side 0 [15]
    pull            side 0 [3]  ; OUT low 60us, 8us (OD); get type of power pull-up
    out  exec, 16   side 1      ; high (via strong or weak pull-up); 2 cycles
    in   null, 1    side 1
.wrap_target
    push            side 1      ; 6+6us, 0.8+0.8us (OD) high-tail; push 0

;
; touch-1
; TX FIFO: type of power pull-up set on the bus afterwards
;
.program w1_touch1
.define public cycle    20
.define public od_cycle 2
.define public strong   0xff81  ; PIO opcode: set  pindirs, 1 side 1 [15]
.define public weak     0xbf42  ; PIO opcode: nop             side 1 [15]
.side_set 1

    set  pindirs, 1 side 0
    pull            side 0      ; OUT low 4us, 0.4us (OD); get type of power pull-up
    set  pindirs, 0 side 1 [3]  ; IN (high by pull-up resistor)
    in   pins, 1    side 1 [9]  ; sampling at 4+8us, 0.4+0.8us (OD) (low + high)
    out  exec, 16   side 1      ; set strong/weak power pull-up; 17 cycles
.wrap_target
    push            side 1      ; 8+56us, 0.8+5.6us (OD) high-tail; push sampl. result
